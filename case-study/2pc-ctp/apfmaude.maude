---(
apfmaude, scheduler, fault
2024.3.17
)
set show advisories off .

--- fmod TERM-ORDER{X :: TRIV} is
---   protecting EXT-BOOL .
---   protecting CONVERSION .
---   protecting META-LEVEL .

---   vars E F : [X$Elt] .
---   vars Q P : Qid .
---   vars A B : NeTermList .
---   vars C D : TermList .
---   vars T U : Term .

---   op lt : [X$Elt] [X$Elt] -> Bool .
---   eq lt(E, F) = $lt(upTerm(E), upTerm(F)) .
  
---   op $lt : TermList TermList -> Bool .
---   eq $lt(Q, P) = string(Q) < string(P) .
---   eq $lt(Q[A], P) = $lt(Q, P) .
---   eq $lt(Q, P[B]) = $lt(Q, P) or-else Q == P .
---   eq $lt(Q[A], P[B]) =
---     if Q == P then $lt(A, B)
---     else $lt(Q, P)
---     fi .
---   eq $lt(empty, B) = true .
---   eq $lt(C, empty) = false .
---   eq $lt((T, C), (U, D)) =
---     if T == U then $lt(C, D)
---     else $lt(T, U)
---     fi .
--- endfm

fmod ADDRESS is
  pr NAT .

  sort Address .
  subsort Nat < Address .

  --- op null : -> Address .

  --- Address is composed of sequences of natural numbers
  op _._ : Address Address -> Address [assoc prec 10] .

  --- List of Address
  sort AddressList .
  subsort Address < AddressList .
  op nilAL : -> AddressList .
  op _;_ : AddressList AddressList -> AddressList [assoc id: nilAL] .

  --- set of Address for fault-lib
  sort AddressSetF .
  subsort Address < AddressSetF .
  op nullASF : -> AddressSetF .
  op _^;^_ : AddressSetF AddressSetF -> AddressSetF [ctor assoc comm id: nullASF] .

  --- list of Address for fault-lib
  sort AddressListF .
  subsort Address < AddressListF .
  op nilALF : -> AddressListF [ctor] .
  op _^:^_ : AddressListF AddressListF -> AddressListF [ctor assoc id: nilALF] .

endfm

view Address from TRIV to ADDRESS is sort Elt to Address . endv

fmod ACTOR-MODEL is
  pr ADDRESS .
  pr FLOAT .
  pr QID .

  --- ActorConfig represents a soup of actors containing no messages
  sorts Actor Config ActorConfig .  
  sorts Msg ScheduleMsg ActiveMsg .
  sorts ActorType Attribute AttributeSet Content .

  subsorts ScheduleMsg ActiveMsg < Config .
  subsort Actor < ActorConfig < Config .
  subsort Attribute < AttributeSet .

  op null : -> ActorConfig .
  op __ : ActorConfig ActorConfig -> ActorConfig [assoc comm id: null] .
  op __ : Config Config -> Config [assoc comm id: null] .

  --- Messages
  op [_,_] : Float Msg -> ScheduleMsg .
  op [_,_,_,_] : Float Msg Qid Nat -> ScheduleMsg .
  op {_,_} : Float Msg -> ActiveMsg .

  --- Message def for fault
  sorts FaultPotentialMsg FaultReadyMsg .
  subsort FaultPotentialMsg < Config .
  subsort FaultReadyMsg < Config .
  op FP : ScheduleMsg -> FaultPotentialMsg .
  op FR : ScheduleMsg -> FaultReadyMsg .

  op _from_to_ : Content Address Address -> Msg .
  op _to_      : Content Address         -> Msg .

  --- Actors:
  op <_:_|_> : Address ActorType AttributeSet -> Actor [format (ni d d d d d d d)] .

  --- Attributes can be used to add user defined data to an actor
  op mt : -> AttributeSet .
  op _,_ : AttributeSet AttributeSet -> AttributeSet [assoc comm id: mt] .
endfm
view Order from TRIV to ACTOR-MODEL is sort Elt to Msg . endv

mod SCHEDULER is
  pr ACTOR-MODEL .
  --- pr TERM-ORDER{Order} .

  sort ScheduleList .
  subsort ScheduleMsg < ScheduleList .

  sort Scheduler .
  subsort Scheduler < Config .
  op {_|_} : Float ScheduleList -> Scheduler [format (n d d n d d)] .

  op Scheduler : -> ActorType .
  op scheduler : -> Address .
  op nilSL : -> ScheduleList .
  op _;_ : ScheduleList ScheduleList -> ScheduleList [assoc id: nilSL format(d d n d)] .
  op clock :_ : Float -> Attribute .
  op scheduledMsg :_ : ScheduleList -> Attribute .
  op insert : Actor ScheduleMsg -> Actor .
  op insert : ScheduleList ScheduleMsg -> ScheduleList .
  op insertList : Actor ScheduleList -> Actor .
  op insertList : ScheduleList ScheduleList -> ScheduleList .

  --- Emit a message, perform a rule, ad nauseam, until time-limit
  op run : Config Float -> Config .
  op step : Config -> Config [iter] .

  vars gt LIMIT : Float .
  vars t1 t2 : Float .
  vars SL SL' : ScheduleList .
  vars M1 M2 : Msg .
  var C : Config .
  vars A A' A1 A2 : Address .
  var T : ActorType .
  var AS : AttributeSet .
  var CO : Content .
  var AC : ActorConfig .
  var SM : ScheduleMsg .
  var AM : ActiveMsg .
  vars N1 N2 : Nat .
  vars Q1 Q2 : Qid .
  var S : Scheduler .

  eq insert(< scheduler : Scheduler | clock : gt, scheduledMsg : SL >, [ t1 , M1, Q1, N1 ]) =
    < scheduler : Scheduler | clock : gt, scheduledMsg : insert(SL,[ gt + t1 , M1, Q1, N1 ] ) > .
  eq insert(([ t1 , M1, Q1, N1 ] ; SL) , [ t2 , M2, Q2, N2 ]) =
    --- if (t1 < t2) or ((t1 == t2)) and lt(M1,M2) then
    if (t1 < t2) then
      [ t1 , M1, Q1, N1 ] ; insert(SL, [ t2 , M2 , Q2, N2 ])
    else
      ([ t2 , M2 , Q2, N2 ] ; [ t1 , M1 , Q1, N1 ]  ; SL)
    fi .
  eq insert(nilSL , [ t2 , M2 , Q2, N2 ]) = [ t2 , M2, Q2, N2 ] .

  --- insert a schedule msg to the scheduler 
  eq [ t1, M1 , Q1, N1 ] < scheduler : Scheduler | clock : gt, scheduledMsg : SL >
      = insert(< scheduler : Scheduler | clock : gt, scheduledMsg : SL >, [ t1, M1 , Q1, N1 ]) .

  eq step(C < scheduler : Scheduler | clock : gt, scheduledMsg : ([ t1 , M1 , Q1, N1] ; SL) > ) 
      = C < scheduler : Scheduler | clock : t1, scheduledMsg : SL >
      FP([ t1 , M1 , Q1, N1]) .
  
  --- finish before limit
  ---(
  eq run(AC < scheduler : Scheduler | clock : gt, scheduledMsg : SL >, LIMIT) =
    if (gt <= LIMIT and SL =/= nilSL) then
      run(step(AC < scheduler : Scheduler | clock : gt, scheduledMsg : SL >), LIMIT)
    else
      AC < scheduler : Scheduler | clock : gt, scheduledMsg : SL >
    fi .
  )

  --- eagerness
 ceq run(C < scheduler : Scheduler | clock : gt, scheduledMsg : SL >, LIMIT) =
    (if (gt <= LIMIT and SL =/= nilSL) then
      run(step(C < scheduler : Scheduler | clock : gt, scheduledMsg : SL >), LIMIT)
    else
      C < scheduler : Scheduler | clock : gt, scheduledMsg : SL >
    fi)
    if not eagerEnabled(C) .
  
  op eagerEnabled : Config -> Bool .
  eq eagerEnabled(AC S) = eagerEnabled(AC) .
  eq eagerEnabled(AC) = false [owise] .

  --- Propagate Msgs
  op pd : -> Float .  --- propagate delay
  vars O O' : Address . var OS : AddressList . var MSG : Msg .
  op propagate_from_to_ : Content Address AddressList -> Config . 
  eq propagate CO from O' to (O ; OS) = 
    [ pd, CO from O' to O ] (propagate CO from O' to OS) .
  eq propagate CO from O' to nilAL = null .

endm


mod APFMAUDE is
  pr SCHEDULER .

  --- Operations supported by PVeStA
  op initState : -> Config .
  op sat : Nat Config -> Bool .
  op val : Nat Config -> Float .
  op tick : Config -> Config .
  op getTime : Config -> Float .
  op LIMIT : -> Float .

  var C : Config .
  var gt : Float .
  var SL : ScheduleList .

  eq tick(C) = run(C, LIMIT) .
  eq getTime(C < scheduler : Scheduler | clock : gt, scheduledMsg : SL >) =  gt .
endm